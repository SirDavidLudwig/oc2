plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.11.0-SNAPSHOT" apply false
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

def getGitRef() {
    try {
        final def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (final Throwable ignored) {
        return 'unknown'
    }
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    configurations {
        embed
        implementation.extendsFrom embed
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.officialMojangMappings()

        testImplementation 'org.mockito:mockito-inline:4.3.1'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = "${rootProject.semver}+${getGitRef()}"
    group = rootProject.maven_group

    repositories {
        repositories {
            mavenCentral()
            maven {
                url 'https://cursemaven.com'
                content { includeGroup 'curse.maven' }
            }
            maven {
                url 'https://dvs1.progwml6.com/files/maven/'
                content { includeGroup 'mezz.jei' }
            }
            ["fnuecke/SednaMinecraft"].forEach(repo -> {
                maven {
                    url = uri("https://maven.pkg.github.com/${repo}")
                    credentials {
                        username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                        password = project.findProperty("gpr.key") ?: System.getenv("GPR_KEY")
                    }
                }
            })
        }
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = System.getenv("GITHUB_MAVEN_URL") ?: ""
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }

    task packageScripts(type: Zip) {
        archiveFileName = 'scripts.zip'
        destinationDirectory = file("$buildDir/resources/main/data/oc2/file_systems")
        from 'common/src/main/scripts'
    }

    task copyLicensesToResources(type: Copy) {
        from '.'
        into file("$buildDir/resources/main")
        include "LICENSE*"
    }

    processResources.dependsOn(packageScripts)
    processResources.dependsOn(copyLicensesToResources)

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}

curseforge {
    apiKey = System.getenv('CURSEFORGE_API_KEY') ?: ""
    project {
        id = curse_project_id
        releaseType = System.getenv('CURSEFORGE_RELEASE_TYPE') ?: "alpha"
        changelogType = 'markdown'
        changelog = System.getenv("CHANGELOG") ?: "Changelog not available."
        addGameVersion 'Forge'
        addGameVersion minecraft_version
        addGameVersion 'Java 17'
    }
}

idea {
    module {
        for (final String exclude in ['out', 'logs', 'run', 'src/generated']) {
            excludeDirs += file(exclude)
        }
    }
}
